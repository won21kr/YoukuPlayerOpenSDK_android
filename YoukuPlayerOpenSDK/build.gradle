buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-plugin:1.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.0"

    defaultConfig {
        applicationId "com.youku.player.ui"
        minSdkVersion 8
        targetSdkVersion 21
        versionCode property('VERSION_CODE').toInteger()
        versionName property('VERSION_NAME')
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        androidTest.setRoot('tests')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.alibaba:fastjson:1.1.33.android'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

/// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 生成Javadoc ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// 窃取自 http://stackoverflow.com/questions/23297562/gradle-javadoc-and-android-documentation#answer-24026735
android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc,
            dependsOn: "extract${variant.name.capitalize()}Annotations") {
        failOnError = false
        options.encoding = 'UTF-8'
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        //classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += files(variant.javaCompile.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 发布 ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
apply plugin: 'android-maven'

task androidJavadocsJar(type: Jar, dependsOn: 'generatereleaseJavadoc')
androidJavadocsJar.doFirst {
    classifier = 'javadoc'
    from generatereleaseJavadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

/*uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        // Add other pom properties here if you want (developer details / licenses)
        //repository(url: "file://${localReleaseDest}")
    }
}*/

if(!hasProperty('BINTRAY_USER') || !hasProperty('BINTRAY_KEY')) return

apply plugin: 'com.jfrog.bintray'

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    configurations = ['archives']
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        userOrg = 'orange'
        name = "${project.group}:${project.name}"
        websiteUrl = 'http://open.youku.com/down#on_android'
        issueTrackerUrl = 'https://github.com/OrangeTeam/YoukuPlayerOpenSDK_android/issues'
        vcsUrl = 'https://github.com/OrangeTeam/YoukuPlayerOpenSDK_android.git'
        licenses = ['Public Domain']
        labels = ['youku', 'Android']
        attributes = ['plat': ['Android']]
        publicDownloadNumbers = true
        /*version {
            name = '1.3.x-Final' //Optional logical version name
            desc = 'optional, version-specific description'
            vcsTag = '1.3.0'
            attributes = ['a': ['ay1', 'ay2', 'ay3'], 'b': ['bee'], c: 'cee']
        }*/
    }
}
